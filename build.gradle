group 'javan-warty-pig'
version '0.1.0'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    repositories {
        mavenCentral()
    }
}

project(':agent') {
    apply plugin: 'com.github.johnrengelman.shadow'
    dependencies {
        compile project(':fuzz')

        testCompile 'junit:junit:4.12'
    }
    build.dependsOn 'shadowJar'
    shadowJar {
        relocate 'org.objectweb.asm', 'jwp.agent.shade.org.objectweb.asm'
        baseName = 'jwp-agent'
        classifier = null
        version = null
        manifest {
            attributes 'Premain-Class': 'jwp.agent.AgentBootstrap'
            attributes 'Can-Redefine-Classes': 'true'
            attributes 'Can-Retransform-Classes': 'true'
        }
    }
    test {
        dependsOn 'shadowJar'
        jvmArgs '-javaagent:build/libs/jwp-agent.jar'
        outputs.upToDateWhen { false }
        testLogging.showStandardStreams = true
    }
}

project(':fuzz') {
    dependencies {
        compile 'org.ow2.asm:asm:6.0'
        compile 'org.ow2.asm:asm-commons:6.0'
        compile 'org.ow2.asm:asm-util:6.0'

        // Optional
        compileOnly 'com.squareup:javapoet:1.9.0'

        testCompile 'junit:junit:4.12'
    }
    test {
        outputs.upToDateWhen { false }
        testLogging.showStandardStreams = true
    }
}

project(':examples') {
    subprojects {
        apply plugin: 'application'
        run.systemProperties System.properties.findAll { it.key.startsWith("jwp.") }
        run.dependsOn ':agent:shadowJar'
        dependencies {
            compileOnly project(':agent')
        }
    }
}

project(':examples:simple') {
    mainClassName = 'jwp.examples.simple.Main'
    run {
        jvmArgs '-javaagent:../../agent/build/libs/jwp-agent.jar'
    }
}
